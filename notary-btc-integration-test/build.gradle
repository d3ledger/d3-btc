buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'com.avast.gradle.docker-compose' version '0.7.1'
}

apply plugin: "kotlin-spring" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin

dockerCompose {
    useComposeFiles = ['../deploy/docker-compose.dev.yml', '../deploy/docker-compose.yml']
}

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs += 'src/integration-test/kotlin'
        }
        resources {
            srcDirs = ["src/integration-test/resources"]
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs Bitcoin integration tests.'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    setWorkingDir("$rootDir/")
    // Enable JUnit5 tests
    useJUnitPlatform {
    }
}

dependencies {
    compile project(":btc-registration")
    compile project(":btc-address-generation")
    compile project(":btc-deposit")
    compile project(":btc-withdrawal")
    compile project(":btc-dw-bridge")

    compile "com.github.d3ledger.notary:notary-registration:$notary_version"
    compile "com.github.d3ledger.notary:notary-iroha-integration-test:$notary_version"
}